FRONT_THICKNESS = 1;
WALL_THICKNESS = 2;
MIDDLE_HEIGHT = 6;
FRONT_HEIGHT = 1;
BACK_HEIGHT = 2;

WIDTH = 120;
LENGTH = 195;
CORNER_DIA = 15;

TOUCHSCREEN_WIDTH = 99.6;
TOUCHSCREEN_LENGTH = 164.6;
TOUCHSCREEN_OFFSET_X = 7 - (WIDTH - TOUCHSCREEN_WIDTH)/2;
TOUCHSCREEN_OFFSET_Y = 17 - (LENGTH - TOUCHSCREEN_LENGTH)/2;
TOUCHSCREEN_RIBBON_WIDTH = 3;
TOUCHSCREEN_RIBBON_LENGTH = 50;
TOUCHSCREEN_RIBBON_OFFSET_X = 4 - (WIDTH - TOUCHSCREEN_RIBBON_WIDTH)/2;
TOUCHSCREEN_RIBBON_OFFSET_Y = 115 - (LENGTH - TOUCHSCREEN_RIBBON_LENGTH)/2;

DISPLAY_WIDTH = 96.6;
DISPLAY_LENGTH = 164.6;
DISPLAY_OFFSET_X = 12 - (WIDTH - DISPLAY_WIDTH)/2;
DISPLAY_OFFSET_Y = 17 - (LENGTH - DISPLAY_LENGTH)/2;
DISPLAY_RIBBON_WIDTH = 5;
DISPLAY_RIBBON_LENGTH = 40;
DISPLAY_RIBBON_OFFSET_X = 90 - (WIDTH - DISPLAY_RIBBON_WIDTH)/2;
DISPLAY_RIBBON_OFFSET_Y = 78 - (LENGTH - DISPLAY_RIBBON_LENGTH)/2;

PORT_WIDTH = 89.6;
PORT_LENGTH = 152.6;
PORT_OFFSET_X = 21 - (LENGTH - PORT_LENGTH)/2;
PORT_OFFSET_Y = 15 - (WIDTH - PORT_WIDTH)/2;

MOUNT_HOLE_DIA = 4;
MOUNT_HOLE_INSERT_DIA = 5;
MOUNT_HOLE_SPACING_X = 109;
MOUNT_HOLE_SPACING_Y = 184;

CONTROLLER_HOLE_DIA = 4;
CONTROLLER_HOLE_SPACING_X = 78;
CONTROLLER_HOLE_SPACING_Y = 115;


FRONT_HOLES = false;

$fn = 50;

module holes(w, l, h, dia) {
  for(i = [-1, 1]) {
    for(j = [-1, 1]) {
      translate([i * w/2, j * l/2, 0])
      cylinder(d = dia, h = h);
    }
  }
}

module rounded_rect(w, l, h, dia) {
    hull()
    holes(w - dia, l - dia, h, dia);
}

module port(w, l, h) {
  translate([-w/2, -l/2, 0])
  cube(size = [w, l, h]);
}

module front() {
  difference() {
    rounded_rect(WIDTH, LENGTH, FRONT_HEIGHT, CORNER_DIA);

    #translate([PORT_OFFSET_X, PORT_OFFSET_Y, 0])
    port(PORT_WIDTH, PORT_LENGTH, FRONT_HEIGHT);

    if(FRONT_HOLES) {
      #holes(MOUNT_HOLE_SPACING_X, MOUNT_HOLE_SPACING_Y, FRONT_HEIGHT, MOUNT_HOLE_DIA);
      #holes(MOUNT_HOLE_SPACING_X, 0, FRONT_HEIGHT, MOUNT_HOLE_DIA);
    }
  }
}

module middle() {
  difference() {
    rounded_rect(WIDTH, LENGTH, MIDDLE_HEIGHT, CORNER_DIA);

    #translate([DISPLAY_OFFSET_X, DISPLAY_OFFSET_Y, 0])
    port(DISPLAY_WIDTH, DISPLAY_LENGTH, MIDDLE_HEIGHT);

    #translate([TOUCHSCREEN_OFFSET_X, TOUCHSCREEN_OFFSET_Y, 0])
    port(TOUCHSCREEN_WIDTH, TOUCHSCREEN_LENGTH, MIDDLE_HEIGHT);

    #translate([TOUCHSCREEN_RIBBON_OFFSET_X, TOUCHSCREEN_RIBBON_OFFSET_Y, 0])
    port(TOUCHSCREEN_RIBBON_WIDTH, TOUCHSCREEN_RIBBON_LENGTH, MIDDLE_HEIGHT);

    #holes(MOUNT_HOLE_SPACING_X, MOUNT_HOLE_SPACING_Y, MIDDLE_HEIGHT, MOUNT_HOLE_INSERT_DIA);
    #holes(MOUNT_HOLE_SPACING_X, 0, MIDDLE_HEIGHT, MOUNT_HOLE_INSERT_DIA);
  }
}

module back() {
  difference() {
    rounded_rect(WIDTH, LENGTH, BACK_HEIGHT, CORNER_DIA);

    #translate([TOUCHSCREEN_RIBBON_OFFSET_X, TOUCHSCREEN_RIBBON_OFFSET_Y, 0])
    port(TOUCHSCREEN_RIBBON_WIDTH, TOUCHSCREEN_RIBBON_LENGTH, BACK_HEIGHT);

    #translate([DISPLAY_RIBBON_OFFSET_X, DISPLAY_RIBBON_OFFSET_Y, 0])
    port(DISPLAY_RIBBON_WIDTH, DISPLAY_RIBBON_LENGTH, BACK_HEIGHT);

    #holes(MOUNT_HOLE_SPACING_X, MOUNT_HOLE_SPACING_Y, BACK_HEIGHT, MOUNT_HOLE_DIA);
    #holes(MOUNT_HOLE_SPACING_X, 0, BACK_HEIGHT, MOUNT_HOLE_DIA);

    #holes(CONTROLLER_HOLE_SPACING_X, CONTROLLER_HOLE_SPACING_Y, BACK_HEIGHT, CONTROLLER_HOLE_DIA);
  }
}

!union() {
  front();
  translate([0, 0, FRONT_HEIGHT])
  middle();
}

back();
